pseudo testall 'exec /zada/olivine/testall.olv'
pseudo xclear 'clear -x'
pseudo pwd 'echo !path'

FUNC echo
    FOR i !(range !#)
        print '!!i '
    END
    print \n
END

FOR dir '/bin/commands' '/bin/fatpath'
    FOR e !(find -f !dir)
        pseudo !(name !e) 'go !e'
    END
END

FUNC show
    echo argc: !#
    IF !#
        FOR i !(range 0 !#)
            echo arg[!i']:' !!i
        END
    END
END

FUNC upper
    IF !(eval !# ~ 1)
        echo upper: require one argument, !# given
        RETURN
    END

    RETURN !(rep '!0' abcdefghijklmnopqrstuvwxyz ABCDEFGHIJKLMNOPQRSTUVWXYZ)
END

FUNC tree
    IF !(eval !# = 0)
        set 0 .
    END

    echo !0

    FOR d !(find -d !0)
        tree !d
    END

    FOR f !(find -f !0)
        echo !f
    END
END

FUNC time
    IF !(eval !# ~ 1)
        echo TIME: require one argument, !# given
        RETURN
    END

    set debut !(ticks)

    !0

    echo !0 took !(eval !(ticks) - !debut) ms
END

FUNC mv
    IF !(eval !# ~ 2)
        echo mv: require two arguments, !# given
        RETURN
    END

    cp !0 !1
    IF !(eval !exit = 0)
        rm !0
    END
END

FUNC redir
    IF !(eval !# ~ 2)
        echo redir: require two arguments, !# given
        RETURN
    END

    mkfile !0

    ubr /dev/stdout !0
    !1
    ubr /dev/stdout /dev/panda
END

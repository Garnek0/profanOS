pseudo testall 'exec /zada/olivine/testall.olv'
pseudo xclear  'clear -x'
pseudo dlg     '. /bin/x/deluge.elf'
pseudo leaks   '. /bin/x/deluge.elf -m'


FUNC pwd
    echo !path
END

FUNC help
    IF !#
        !0 -h
        RETURN
    END
    echo -e < /zada/common/shell_help.txt
END

FUNC name
    IF !(eval !# ~ 1)
        print 'Usage: name <path>\n'
        RETURN
    END
    set n !(split '!0' / -1)
    FOR i !(range !(eval !(strlen !n) - 1) 0)
        IF !(eval "!n" @ !i = ".")
            RETURN !(split !n '' 0:!i)
        END
    END
    RETURN !n
END

FUNC dirsize
    IF !(eval !# = 0)
        set 0 !path
        set ++ #
    END

    FOR i !(range !#)
        set size 0

        FOR f !(search -f !!i |)
            set size !(eval !size + !(fsize !f))
        END

        IF !(eval !size > 10000)
            set size !(eval !size / 1000 . "kB")
        END; ELSE
            set size !(eval !size . "B")
        END
        echo '!!i': !size
    END
END

FUNC time
    IF !(eval !# = 0)
        print 'Usage: time <command> [arg1] [arg2] ...\n'
        RETURN
    END

    set cmd ''
    FOR i !(range !#)
        set cmd '!cmd !!i'
    END

    set debut !(ticks)

    !cmd

    print '!0 took !(eval !(ticks) - !debut) ms\n'
END
